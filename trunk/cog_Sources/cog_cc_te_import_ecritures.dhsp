
;*
;*	COGESER - Intégration des écritures comptables et/ou du plan comptable depuis Excel
;*

;*		Log	 Version	  N°modIf		Date		Modifications
;*
;*		JDT		6.3		01				03/2010	Version initiale : traitement des lignes d'écritures, des lignes de ventilations et/ou du plan comptable.

OverWrittenBy "COGuCC_TE_IMPORT_ECRITURES.DHOF"

Include	"CCTC000.DHSP"
Module	"GTPMFIC.dhop"

; Fichiers
Public HFile	GTFDD.DHSD	GTFDOS			GTFDOS		; Dossier établissement

; Enregistrements
Public Record	A5DD.DHSD		M2				M2
Public Record	GTFDD.DHSD		Soc			Soc			; Société
Public Record	GTFDD.DHSD		Ets			Ets			; Etablissement

Define cog_Masque = "COG_CC_EE_IMPORT_ECRITURES.DHOF"
1 idTB_Main			L		; Identifiant barre d'outils MAIN
1 idME_Main			L		; Identifiant du menu MAIN 

1	b_FIN				B = False 		; Variable de fin de saisie du masque

;* 01
Public Function char COGF_RAFRAICHIR
1	t_Serveur						50			; Serveur DDE
1	t_Topic							50			; Objet destinataire (system,feuille)
1	t_IdServ							X			; Excel
1	t_IdServF						X			; Feuille du classeur
1	t_Item							255		; Nom de la cellule
1	x_st_00							X			; Pointeur	
1	t_Retour							255		; Retour
BeginF
	; Initialisation
	t_Retour = " "
	Harm_Init(60,60,60,60,60)
	If DDEOpen = 0
	   t_Retour = "Erreur d'initialisation (DDE Open)."
	Else
		t_Serveur	= "Excel"
		t_Topic		= "system"
		t_IdServ		= DDEConnect(t_Serveur, t_Topic) T
		If t_IdServ = 0
			t_Retour = "Erreur lors de l'accès à l'application Excel (DDE Open)."
		Else
		   t_Serveur	= "Excel"			   
		   t_Topic		= "COGESER - CCFM"
		   t_IdServF	= DDEConnect(t_Serveur, t_Topic)
		   If t_IdServF <> 0   		
				t_Retour = 'Connexion avec la feuille "COGESER - CCFM" établie.'
				; Déconnexion
				x_st_00 = DDEDeconnect(t_IdServF)
			Else
				t_Retour = 'Erreur de connexion avec la feuille "COGESER - CCFM".'
			EndIf																
		EndIf	
		; Déconnexion
		x_st_00 = DDEDeconnect(t_IdServ)
		x_st_00 = DDEClose 
	EndIf
	FReturn(t_Retour)
EndF

;*
Main
	
; Initialisation
If PingReceive("ZEchange",MZ) <> 0
	ProgramExit
EndIf

; Initialisation des variables
Seek_Soc(MZ.Dos)		; Contrôle du dossier
Seek_Ets(MZ.Etb)

; Chargement d'un fichier masque
XMeLoad(cog_Masque)

;Titre fenêtre
XMeTitle("DIVALTO Comptabilité \ Importation des écritures comptables et/ou du plan comptable")

; Renseigne dans des variables systèmes les paramètres de la pages 1
XMeInfoPage(cog_Masque, 1)

; Chargement en mémoire d'une barre d'outils 
idTB_Main = XMeToolBarGetID(cog_Masque, "MAIN")
; Attachement d'une barre d'outils à une page de masque, pour préparer son affichage
XMeToolBarSetWindow(idTB_Main, cog_Masque, 1)
; Chargement en mémoire d’un menu classique ou surgissant
idME_Main = XmeMenuGetId(cog_Masque, "MAIN")
; Attachement d’un menu classique à une page de masque, pour préparer son affichage
XmeMenuSetWindow(idME_Main,cog_Masque,1)
									 
; Initialisation des variables
	; Dossier
C3XQ.Dos = MZ.Dos
	; Etablissement
C3XQ.Etb = MZ.Etb
	; Mode d'importation
C3XQ.Choix(1) = 1
	; Fichier
C3XQ.Fic = COGF_RAFRAICHIR

; Lancement de la saisie d'un masque
Xmeinput(cog_Masque, 1,0, XMe_Mode_Goto)

; Boucle sur la saisie	
Do
	Switch Harmony.Arret

		Case Harmony.Arret = 19997
			; Livre de bord
			Zoom_Call(Harmony.Arret)
		
		Case Harmony.Arret = 19106
			; Ecritures
			Zoom_Call(Harmony.Arret)

		Case STATIN_F9  ;Abandon
			b_FIN = True
		
		Case STATIN_F5 Or Harmony.Arret = 1001
			; Actualisation
			C3XQ.Fic = COGF_RAFRAICHIR

		Case STATIN_F10 Or Harmony.Arret = 1002
			; 
			If Left(COGF_RAFRAICHIR) = 'Connexion avec la feuille "COGESER - CCFM" établie.'
				If MessageBox("Vous allez lancer l'importation des écritures comptables.|Voulez vous continuer ?", "Importation des Ecritures Comptables", MB_YESNO + MB_ICONQUESTION) = IDYES
					; Exécution du programme
					MZ.Echange = "COGP_Edition_CompteRendu"	; Pour l'impression du compte rendu
					Ping("ZEchange",MZ)
					ProgramCall("cog_cc_tt_import_ecritures.dhop")
					MessageBox("Traitement terminé.", "Importation des Ecritures Comptables", MB_OK + MB_ICONINFORMATION)
					;b_FIN = TRUE
				EndIf
			EndIf
			; Reprise de la saisie du masque
			System.Statin = 0
			Harmony.Arret = 0

;		Case STATIN_F12
;			b_FIN = TRUE

		Case Statin_F8						; Appel de zoom
			Zoom_Call(Harmony.Arret)

		Case Statin_F7
			Zoom_Call()

		Case Harmony.DataArret = 8002
			If Harmony.Sourisbout = Right_Button
				Zoom_Call()
			ElsIf Harmony.Sourisclic = Double_Click
				Harmony.Retour = Xmenext_Simulation_Touche
				Harmony.Cplretour = C_F8
			EndIf

		Default
			
		EndSwitch
While b_FIN = False
	; Reprise de la saisie du masque
	XmeNext(Harmony.RETOUR, Harmony.CPLRETOUR)
Wend

If b_FIN = TRUE
	; Suppression ou message livre de bord
EndIf

ProgramExit

